plugins {
    id 'java'
    id 'war'
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'itmo'
version = '0.0.1-SNAPSHOT'
description = 'PopulationService'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'wsdl4j:wsdl4j:1.6.3'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

configurations.configureEach {
    exclude group: 'org.apache.httpcomponents.client5', module: 'httpclient5'
}

bootWar {
    enabled = true
    archiveClassifier = 'boot'
}

war {
    enabled = true
    archiveClassifier = ''
}

tasks.named('test') {
    useJUnitPlatform()
}

// Конфигурация для генерации классов из XSD
configurations {
    jaxb
}

// Задача для генерации Java классов из XSD
task genJaxb {
    doLast {
        project.mkdir('src/main/generated')

        ant.taskdef(
                name: 'xjc',
                classname: 'com.sun.tools.xjc.XJCTask',
                classpath: configurations.jaxb.asPath
        )

        ant.xjc(
                destdir: 'src/main/generated',
                schema: 'src/main/resources/xsd/population-service.xsd',
                package: 'itmo.populationservice.soap'
        )
    }
}

// Добавляем сгенерированные источники
sourceSets {
    main {
        java {
            srcDirs += ['src/main/generated']
        }
    }
}

// Зависимости для JAXB генерации
dependencies {
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.3.1'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.3.1'
    jaxb 'javax.xml.bind:jaxb-api:2.3.1'
    jaxb 'com.sun.istack:istack-commons-runtime:3.0.7'
}

// Генерация классов перед компиляцией
compileJava.dependsOn genJaxb
